package lab4_pak
public
	with Base_Types; -- np. Base_Types::Character
	with Data_Model;
	
	
  data TemperatureType
     properties
       Data_Model::Data_Representation => Integer;
  end TemperatureType;
  
  data HeaterControlType
    properties
      Data_Model::Data_Representation => Boolean;
  end HeaterControlType;
  
  data ACControlType
    properties
      Data_Model::Data_Representation => Boolean;
  end ACControlType;
  
  data SetPointType
    properties
      Data_Model::Data_Representation => Integer;
  end SetPointType;
  
  device TempSensor
    features
      Temp : out data port TemperatureType;
  end TempSensor;

  device implementation TempSensor.impl
  end TempSensor.impl;
  
  device Heater
    features
      Power : in data port HeaterControlType;
  end Heater;

  device implementation Heater.impl
  end Heater.impl;
  
  device AirConditioner
    features
      Cooling : in data port ACControlType;
  end AirConditioner;

  device implementation AirConditioner.impl
  end AirConditioner.impl;
  
  device Display
    features
      CurrentTemp : in data port TemperatureType;
      SetTemp     : in data port SetPointType;
  end Display;

  device implementation Display.impl
  end Display.impl;

  device SetPointInput
    features
      SetTemp : out data port SetPointType;
  end SetPointInput;

  device implementation SetPointInput.impl
  end SetPointInput.impl;
  
  thread TempReader
    features
      TempIn : in data port TemperatureType;
      TempOut: out data port TemperatureType;
    properties
      Dispatch_Protocol => Periodic;
      Period => 800ms;
      Compute_Execution_Time => 0ms..2ms;
  end TempReader;

  thread implementation TempReader.impl
  end TempReader.impl;
  
  
  thread ControlLogic
    features
      Temp    	: in data port TemperatureType;
      SetTemp   : in data port SetPointType;
      HeatCmd 	: out data port HeaterControlType;
      ACCmd     : out data port ACControlType;
      SetTempOut: out data port SetPointType;
    properties
      Dispatch_Protocol => Periodic;
      Period => 800ms;
      Compute_Execution_Time => 0ms..2ms;
  end ControlLogic;

  thread implementation ControlLogic.impl
  end ControlLogic.impl;
  
  
  thread HeaterDriver
    features
      PowerIn : in data port HeaterControlType;
      PowerOut: out data port HeaterControlType;
    properties
      Dispatch_Protocol => Periodic;
      Period => 800ms;
      Compute_Execution_Time => 0ms..2ms;
  end HeaterDriver;

  thread implementation HeaterDriver.impl
  end HeaterDriver.impl;
  
  thread ACDriver
    features
      ACIn  : in data port ACControlType;
      ACOut : out data port ACControlType;
    properties
      Dispatch_Protocol => Periodic;
      Period => 800ms;
      Compute_Execution_Time => 0ms..2ms;
  end ACDriver;
  
  thread implementation ACDriver.impl
  end ACDriver.impl;
  
  process Controller
  	features
    SensorIn  	: in data port TemperatureType;
    SetPointIn 	: in data port SetPointType;
    HeaterOut 	: out data port HeaterControlType;
    ACOut      	: out data port ACControlType;
    TempOut    	: out data port TemperatureType;
    SetOut     	: out data port SetPointType;
  end Controller;

  process implementation Controller.impl
    subcomponents
      TempReader : thread TempReader.impl;
      Control    : thread ControlLogic.impl;
      HeaterCtrl : thread HeaterDriver.impl;
      ACCtrl	 : thread ACDriver.impl;
    connections
      conn1 : port SensorIn -> TempReader.TempIn;
      conn2 : port TempReader.TempOut -> Control.Temp;
      conn3 : port SetPointIn -> Control.SetTemp;
      conn4 : port Control.HeatCmd -> HeaterCtrl.PowerIn;
      conn5 : port Control.ACCmd -> ACCtrl.ACIn;
      conn6 : port HeaterCtrl.PowerOut -> HeaterOut;
      conn7 : port ACCtrl.ACOut -> ACOut;
      conn8 : port TempReader.TempOut -> TempOut;
      conn9 : port Control.SetTempOut -> SetOut;
  end Controller.impl;
  
  
  system TempControlSystem
  end TempControlSystem;

  system implementation TempControlSystem.impl
    subcomponents
      Sensor  : device TempSensor.impl;
      Heater  : device Heater.impl;
      Cooler  : device AirConditioner.impl;
      Display    : device Display.impl;
      SetPoint   : device SetPointInput.impl;
      Control : process Controller.impl;
    connections
	  s_to_c : port Sensor.Temp -> Control.SensorIn;
	  set_to_c   : port SetPoint.SetTemp -> Control.SetPointIn;
      c_to_h : port Control.HeaterOut -> Heater.Power;
	  c_to_ac : port Control.ACOut -> Cooler.Cooling;
	  c_to_disp1 : port Control.TempOut -> Display.CurrentTemp;
      c_to_disp2 : port Control.SetOut  -> Display.SetTemp;
  end TempControlSystem.impl;
	
end lab4_pak;